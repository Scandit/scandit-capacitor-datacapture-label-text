ext {
    androidxCoreVersion = project.hasProperty('androidxCoreVersion') ? rootProject.ext.androidxCoreVersion : '1.10.0'
    androidxAppCompatVersion = project.hasProperty('androidxAppCompatVersion') ? rootProject.ext.androidxAppCompatVersion : '1.6.1'
    cordovaAndroidVersion =  project.hasProperty('cordovaAndroidVersion') ? rootProject.ext.cordovaAndroidVersion : '10.1.1'
}

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
    def getFrameworksFile = { String rootDir ->
        def currentDir = new File(rootDir)

        while (currentDir != null) {
            def frameworksFile = new File(currentDir, "common/frameworks.gradle")
            if (frameworksFile.exists()) {
                return frameworksFile.absolutePath
            }
            currentDir = currentDir.getParentFile()
        }

        return null
    }


    def getCapacitorMajorVersion = {
        try {
            def capacitorAndroidDir = new File(rootProject.projectDir, "node_modules/@capacitor/android")
            if (capacitorAndroidDir.exists()) {
                def packageJson = new File(capacitorAndroidDir, "package.json")
                if (packageJson.exists()) {
                    def json = new groovy.json.JsonSlurper().parseText(packageJson.text)
                    def version = json.version
                    if (version) {
                        def majorVersion = version.tokenize('.')[0] as Integer
                        return majorVersion
                    }
                }
            }
        } catch (Exception e) {
            // Fallback if detection fails
        }
        return 7 // Default to Capacitor 7 if detection fails
    }

    def retrieveCapacitorCompatibility = {
        def appModule = rootProject.findProject(":app")
        if (appModule == null) {
            return
        }

        def file = appModule.file("capacitor.build.gradle")
        if (file.exists()) {
            def content = file.text

            def sourceCompatibilityMatch = content =~ /sourceCompatibility\s+JavaVersion.VERSION_(\d+)/
            def targetCompatibilityMatch = content =~ /targetCompatibility\s+JavaVersion.VERSION_(\d+)/

            if (sourceCompatibilityMatch) {
                ext.rootSourceCompatibility = JavaVersion.toVersion(sourceCompatibilityMatch[0][1])
            } else {
                ext.rootSourceCompatibility = JavaVersion.VERSION_17
            }

            if (targetCompatibilityMatch) {
                ext.rootTargetCompatibility = JavaVersion.toVersion(targetCompatibilityMatch[0][1])
            } else {
                ext.rootTargetCompatibility = JavaVersion.VERSION_17
            }
        }
    }

    ext {
        rootSourceCompatibility = JavaVersion.VERSION_17
        rootTargetCompatibility = JavaVersion.VERSION_17
    }

    // Check for monorepo frameworks.gradle
    def common_gradle_script = getFrameworksFile(rootProject.projectDir.path)
    if (common_gradle_script != null) {
        apply from: common_gradle_script
    }

    retrieveCapacitorCompatibility()

    // Set AGP version if not already defined by frameworks.gradle or root project
    if (!ext.has('agpVersion') && !rootProject.ext.has('agpVersion')) {
        def capacitorMajorVersion = getCapacitorMajorVersion()
        ext.agpVersion = capacitorMajorVersion >= 8 ? '8.13.0' : '8.7.2'
    }

    // Use repos from frameworks.gradle if available, otherwise explicit repos
    if (ext.has('repos')) {
        repositories ext.repos
    } else {
        repositories {
            google()
            mavenCentral()
        }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$agpVersion"
    }
}

apply plugin: 'com.android.library'

android {
    namespace "com.scandit.capacitor.datacapture.label_text"
    compileSdk project.hasProperty('compileSdkVersion') ? rootProject.ext.compileSdkVersion : 33
    defaultConfig {
        minSdk project.hasProperty('minSdkVersion') ? rootProject.ext.minSdkVersion : 24
        targetSdk project.hasProperty('targetSdkVersion') ? rootProject.ext.targetSdkVersion : 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility = rootSourceCompatibility
        targetCompatibility = rootTargetCompatibility
    }
}

// Use repos from frameworks.gradle if available, otherwise explicit repos
if (ext.has('repos')) {
    repositories ext.repos
} else {
    repositories {
        google()
        mavenCentral()
    }
}

dependencies {
    def sdk_version = "8.2.0"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation fileTree(dir: 'library', include: ['*.aar'])
    implementation project(':capacitor-android')

    api "com.scandit.datacapture:label-text-models:${safeExtGet('global_sdk_version', sdk_version)}"

    implementation "androidx.core:core-ktx:$androidxCoreVersion"
}

tasks.register("detekt") {
    // do nothing
    // this is just to make sure the detekt execution will not fail here.
}
